ext.applicationId = "au.com.pjwin.landmarkremark"
ext.versionName = '1.0'
ext.versionCode = 1

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    buildToolsVersion prop("version.build.tools")
    compileSdkVersion prop("version.sdk.max").toInteger()

    defaultConfig {
        minSdkVersion prop("version.sdk.min").toInteger()
        targetSdkVersion prop("version.sdk.max").toInteger()
        versionName project.versionName
        versionCode project.versionCode

        testApplicationId project.applicationId + ".test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        /*externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }*/
        buildConfigField 'String', 'SCHEMA', '""'
        buildConfigField 'String', 'HOST', '""'
        buildConfigField 'int', 'PORT', '443'
        buildConfigField 'String', 'CONTEXT_ROOT', '""'
        buildConfigField 'long', 'CONNECTION_TIMEOUT', '30'
        buildConfigField 'long', 'READ_TIMEOUT', '70'

        //for Kumulos push, leave blank for now
        manifestPlaceholders = [
                kumulos_gcm_sender_id: ''
        ]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"
    productFlavors {
        dev {
            dimension "env"
            resValue 'string', 'MAP_API_KEY', '"AIzaSyBSoY0EfUXURPdWacx3paiTaLMT-aIg-i8"'
            buildConfigField 'String', 'KUMULOS_API_KEY', '"62eb2e45-81ba-4f99-af63-82260bfdbb1b"'
            buildConfigField 'String', 'KUMULOS_SECRET_KEY', '"fjTJbUb6uxRAxi965veOv1RBeE4iD8bEcxzC"'
            buildConfigField 'String', 'KUMULOS_REST_KEY', '"5f412b6ccf661bdda2f563c403f9b99e2a12dd027828d1e3395bb2204394147c"'
        }
        prod {
            dimension "env"
            resValue 'string', 'MAP_API_KEY', '""'
        }
    }

    productFlavors.all {
        applicationId name.startsWith("dev") ? project.applicationId + '.' + name : project.applicationId
    }

    /*externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }*/

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    testOptions {
        unitTests {
            all {
                jvmArgs '-noverify'
            }
            returnDefaultValues true
            includeAndroidResources true
        }
    }
}

dependencies {
    def googlePlayServicesVersion = "15.0.1"
    def fireBaseVersion = "16.0.3"
    def mockitoVersion = prop("version.mockito")
    def robolectricVersion = prop("version.robolectric")
    def kumulosVersion = "4.1.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':common')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation "com.google.android.gms:play-services-maps:$googlePlayServicesVersion"
    implementation "com.google.android.gms:play-services-location:$googlePlayServicesVersion"
    implementation "com.google.android.gms:play-services-places:$googlePlayServicesVersion"

    implementation "com.google.firebase:firebase-core:$fireBaseVersion"
    implementation "com.google.firebase:firebase-auth:$fireBaseVersion"
    implementation 'com.firebaseui:firebase-ui-auth:4.1.0'

    debugImplementation "com.kumulos.android:kumulos-android-debug:$kumulosVersion"
    releaseImplementation "com.kumulos.android:kumulos-android-release:$kumulosVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "org.robolectric:shadows-multidex:$robolectricVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
}

apply plugin: 'com.google.gms.google-services'